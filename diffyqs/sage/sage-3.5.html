<!doctype html>

<html lang=en>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<title>Notes on Diffy Qs - Sage demos for section 3.5</title>
<link rel=stylesheet type="text/css" href="../../basicstyle.css"> 
<link rel=stylesheet type="text/css" href="../../bookpages.css"> 
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="referrer" content="unsafe-url">
<script src="https://polyfill.io/v3/polyfill.min.js?features=es6"></script> 
<script id="MathJax-script" async 
 src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-chtml.js"> 
</script> 
<script src="https://sagecell.sagemath.org/static/jquery.min.js"></script>
<script src="https://sagecell.sagemath.org/static/embedded_sagecell.js"></script>
<script>sagecell.makeSagecell({"inputLocation": ".sage",
template:       sagecell.templates.minimal,
evalButtonText: 'Activate'});
sagecell.makeSagecell({"inputLocation": ".sageedit"});</script>
</head>

<body>

<div class=thebody>

<p class="nojustify left">
[<a href="..">Go to the Notes on Diffy Qs home page</a>]

<h1>Notes on Diffy Qs - Sage demos for section 3.5</h1>

<p>
Press the <em>Activate</em> buttons below to launch the Sage demonstrations.
After the demonstration launches you should be able to interact with it
to change numbers.  You may have to wait a little before the graph
appears.  Be patient.

<h2>Two dimensional linear system</h2>

<p>
Plot the vector field and a solution to
<p>
\(
\left[
\begin{matrix}
x' \\ y'
\end{matrix}
\right]
=
\left[
\begin{matrix}
a &amp; b \\
c &amp; d
\end{matrix}
\right]
\left[
\begin{matrix}
x \\ y
\end{matrix}
\right]
\)
<p>
The eigenvalues are computed and printed, the vector field is drawn, and a solution for \(0 &lt; t &lt; 20\)
is drawn.  You can change the matrix and the initial value of the solution.

<div class="sage">
<script type="text/x-sage">
var(['t','u','v','f','g'])
x = function('x')(t)
y = function('y')(t)
print ("Vector field of [x,y]' = P[x,y] where P = [[a,b],[c,d]].")
print ("Solution starting at x(0) = x0, y(0) = y0 for 0 < t < 20 also shown.")
sys.stdout.flush()
@interact
def interactive_function(a = float(-1),
                         b = float(1),
                         c = float(1),
                         d = float(1),
                         x0 = float(-4),
                         y0 = float(2),
                         auto_update=False):
    P = matrix([[a,b],[c,d]]) #Define the matrix P
    var('E')
    print ("P = ")
    print (N(P,digits=6))
    Ei = solve((a-E)*(d-E) - (b*c), E) #Find the eigenvalues of P
    E1 = Ei[0].rhs()
    if abs((d+a)^2 - (4*(a*d - b*c))) < 0.0000001:
        E2 = E1
        print('Eigenvalue (Multiplicity 2):',N(E1,digits=6))
    else:
        E2 = Ei[1].rhs()
        print('Eigenvalues:',N(E1,digits=6),"and",N(E2,digits=6))
    print ("x(0)= " + str(N(x0,digits=6)) +  ",  y(0)= " + str(N(y0,digits=6)))
    sys.stdout.flush()
    de1 = a*x + b*y == diff(x,t) #Defines the two differential equations in the system
    de2 = c*x + d*y == diff(y,t)
    sol = desolve_system([de1, de2], [x,y], ics = [0,x0,y0]) #Solves the system 
    solx,soly = sol[0].rhs(),sol[1].rhs()
    G = parametric_plot((solx,soly), (t, 0, 20))
    A = a*u + b*v
    B = c*u + d*v
    field = (A,B) #Creates vector field
    H = plot_vector_field( field, (u,-5,5), (v,-5,5), plot_points=21, color='purple', headwidth = 7, headlength = 7 )
    pt = point((x0,y0))
    show(G + H + pt, xmin = -5, xmax = 5, ymin = -5, ymax = 5)
</script>
</div>

<h2>Two dimensional linear system with given eigenvalues</h2>

<p>
Plot the vector field and a solution to
<p>
\(
\left[
\begin{matrix}
x' \\ y'
\end{matrix}
\right]
=
P
\left[
\begin{matrix}
x \\ y
\end{matrix}
\right]
\)
<p>
where \(P\) is some matrix with eigevalues \(a \pm bi,\) or \(a\) and \(c\) (set \(b=0\) to get the two real eigenvalues).
The vector field is drawn, and a solution for \(0 &lt; t &lt; 20\)
is drawn.

<div class="sage">
<script type="text/x-sage">
var(['t','u','v','f','g'])
x = function('x')(t)
y = function('y')(t)
print ("Vector field of [x,y]' = P[x,y]")
print ("If b = 0, P has eigenvalues E1 = a and E2 = c.")
print ("If not, then P has eigenvalues E1 = a + bi and E2 = a - bi.")
print ("Solution starting at x(0) = x0, y(0) = y0 for 0 < t < 20 also shown.")
sys.stdout.flush()
@interact
def interactive_function(a = float(-0.2),
                         b = float(2),
                         c = float(1),
                         x0 = float(3),
                         y0 = float(2),
                         auto_update=False):
    var('E')
    if abs(b) < 0.000001:
        P = matrix([[a,0],[0,c]])
    else:
        P = matrix([[a,b],[-b,a]])
    S = matrix([[2,3],[1,-3]])
    Si = matrix([[1/3, 1/3],[1/9, -2/9]]) 
    P = S * (P * Si) #Complicates P without changing the Eigenvalues
    print ("P = ")
    print (N(P,digits=6))
    if abs(b) < 0.000001:
        Ei = solve((E-a)*(E-c), E)
    else:
        Ei = solve((E-a)^2+b^2, E)
    E1 = Ei[0].rhs()
    if abs(b) < 0.000001 and abs(a-c) < 0.000001:
        E2 = E1
        print('Eigenvalue (Multiplicity 2):',N(E1,digits=6))
    else:
        E2 = Ei[1].rhs()
        print('Eigenvalues:',N(E1,digits=6),"and",N(E2,digits=6))
    print ("x(0)= " + str(N(x0,digits=6)) + ",  y(0)= " + str(N(y0,digits=6)))
    sys.stdout.flush()
    de1 = P[0,0]*x + P[0,1]*y == diff(x,t) #Defines the differential equations in the system
    de2 = P[1,0]*x + P[1,1]*y == diff(y,t)
    sol = desolve_system([de1, de2], [x,y], ics = [0,x0,y0]) #Solves the system of differential equations
    solx,soly = sol[0].rhs(),sol[1].rhs()
    G = parametric_plot((solx,soly), (t, 0, 20))
    A = P[0,0]*u + P[0,1]*v
    B = P[1,0]*u + P[1,1]*v
    field = (A,B) #Creates the vector field
    H = plot_vector_field( field, (u,-5,5), (v,-5,5), plot_points=21, color='purple', headwidth = 7, headlength = 7 )
    pt = point((x0,y0))
    show(G + H + pt, xmin = -5, xmax = 5, ymin = -5, ymax = 5)
</script>
</div>

<hr>
<p>
The original code is mainly due to Ryan Burkhart.

<hr>
<p>
<a href="http://validator.w3.org/check/referer"><img src="/images/valid-html5.png" alt="Valid HTML 5" style="border:0; width:88px; height:31px;"></a>
<a href="http://jigsaw.w3.org/css-validator/check/referer"><img style="border:0;width:88px;height:31px" src="/images/vcss.png" alt="Valid CSS!"></a>

<!-- hack -->
<!--<br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br>
<br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br>
-->
<div style="height:1000px; background-image:linear-gradient(white,black);"></div>

</body>
</html>
