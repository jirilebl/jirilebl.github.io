<!doctype html>

<html lang=en>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<title>Notes on Diffy Qs - Sage demos for section 2.6</title>
<link rel=stylesheet type="text/css" href="../../basicstyle.css"> 
<link rel=stylesheet type="text/css" href="../../bookpages.css"> 
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="referrer" content="unsafe-url">
<script src="https://polyfill.io/v3/polyfill.min.js?features=es6"></script> 
<script id="MathJax-script" async 
 src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-chtml.js"> 
</script> 
<script src="https://sagecell.sagemath.org/static/jquery.min.js"></script>
<script src="https://sagecell.sagemath.org/static/embedded_sagecell.js"></script>
<script>sagecell.makeSagecell({"inputLocation": ".sage",
template:       sagecell.templates.minimal,
evalButtonText: 'Activate'});
sagecell.makeSagecell({"inputLocation": ".sageedit"});</script>
</head>

<body>

<div class=thebody>

<p class="nojustify left">
[<a href="..">Go to the Notes on Diffy Qs home page</a>]

<h1>Notes on Diffy Qs - Sage demos for section 2.6</h1>

<p>
Press the <em>Evaluate</em> buttons below to launch the Sage demonstration.
After the demonstration launches you should be able to interact with it
to change numbers with sliders.  You may have to wait a little before the graph
appears.  Be patient.

<h2>Undamped forced oscillation and resonance</h2>

<p>
In the following demonstration we graph the solution of
<p>
\(\displaystyle \frac{x''}{3} + 3x = 10\cos(\omega t) ,
\qquad x(0) = x'(0) = 0.\)
<p>
You can change the value of \(\omega.\)  Try approaching \(\omega = 3\) to see resonance.
Also notice the 'beating' behaviour when \(\omega\) is not equal to 3.
(Below, \(\omega\) is rendered as w.)

<div class="sageedit">
<script type="text/x-sage">
var('t')
x = function('x')(t)
@interact
def interactive_function(w = slider(.5,6,.0625, default = 3.8)) :
    # checks for resonance, and changes the function accordingly
    if abs(w-3) > 0.001 :
        x = -30*cos(3*t)/(9-(w^2)) + 30*cos(w*t)/(9-(w^2))
        p2 = plot(60*sin((3-w)*t/2)/(9-w^2), (t,0,20)) # plots larger amplitude sine part of product of sines.
        p3 = plot(-60*sin((3-w)*t/2)/(9-w^2), (t,0,20)) # plots the negative of the above to create envelope
        p1 = plot(x,(t,0,20), thickness = 3, color = 'green')
        show(p1 + p2 + p3)
    else :
        x = 10*t*sin(w*t)/(2*w/3)
        p1 = plot(x,(t,0,20), thickness = 3, color = 'green')
        show(p1)
</script>
</div>

<h2>Forced oscillation and practical resonance</h2>

<p>
In the deomnstration below, we graph two solutions of
<p>
\(x'' + cx' + x = \cos(\omega t)\)
<p>
One solution starts at rest, that is \(x(0) = x'(0) = 0,\) and
the other starts at \(x(0) = x_0\) and \(x'(0) = d_0.\)  Notice how quickly the 
<em>transient</em> part of the solution goes away and both solutions settle into
the <em>steady periodic solution</em>.
<p>
We also graph the amplitude \(C(\omega)\) for different \(\omega,\) so that you can find the \(\omega\) that
gives you the maximum practical resonance amplitude.  Note how the graph of \(C(\omega)\) changes when you change
the damping \(c.\)
<p>
(Below, \(\omega\) is rendered as w, \(x_0\) as x0 and \(d_0\) as d0.)

<div class="sageedit">
<script type="text/x-sage">
var('t p omega')
x = function('x')(t)
C = function('C')(omega)
@interact
def interactive_function(w = slider(.25, 3, .01, default = 1.75),
                         c = slider(.02, 2, .01, default = .6),
                         x0 = slider(-5, 5, .01, default = 1.0),
                         d0 = slider(-5, 5, .01, default = -2.0),
                         trange = slider(10, 200, 10, default=30)) :
    de = diff(diff(x,t),t) + c*diff(x,t) + x == cos(w*t) # defines our differential equation
    f1 = desolve(de, x, ics = [0,0,0]) # solves the equation with initial conditions x(0) = 0 and x'(0) = 0
    f2 = desolve(de, x, ics = [0, x0, d0]) # solves the equation with initial conditions x(0) = x0 and x'(0) = d0
    p1 = plot(f1, (t,0,trange), color = 'green', thickness = 3, legend_color = 'green', legend_label = "x(0)=0, x'(0)=0")
    p2 = plot(f2, (t,0,trange), color = 'red', thickness = 2, legend_color = 'red', legend_label = "x(0)=x0, x'(0)=d0")
    print ("Graph of two solutions to x'' + cx' + x = cos(wt)")
    print ("for t in the range [0,trange]:")
    sys.stdout.flush()
    show(p1 + p2)
    p = c/2
    C = 1/(sqrt((2*omega*p)^2 + (1 - omega^2)^2)) # formula for C
    p3 = plot(C, (omega,0,6), thickness = 2) 
    pw1 = line([(w,0),(w,1/(sqrt((2*w*p)^2+(1-w^2)^2)))], thickness = 1, color = 'red') # graphs vertical line at w
    pw2 = line([(0,1/(sqrt((2*w*p)^2+(1-w^2)^2))),(w,1/(sqrt((2*w*p)^2+(1-w^2)^2)))], thickness = 1, color = 'red') # graphs horizontal line at C(w)
    print ("Graph of practical resonance amplitude C with respect to omega,")
    print ("with red lines indicating the given w and corresponding C(w), and")
    print ("green line representing the practical resonance frequency (if any):")
    sys.stdout.flush()
    if 1 > 2*p^2 :
       wr = sqrt(1-2*p^2)
       pw3 = line([(wr,0),(wr,1/(sqrt((2*wr*p)^2+(1-wr^2)^2)))], thickness = 1, color = 'green') # graphs vertical line at resonance frequency
       show(p3 + pw1 + pw2 + pw3, xmin = 0, xmax = 3, ymin = 0)
       print ("The practical resonance is at w = " + str(N(wr,digits=6)) + " (green line)")
    else :
       show(p3 + pw1 + pw2, xmin = 0, xmax = 3, ymin = 0)
       print ("Maximum amplitude occurs at w = 0, that is,")
       print ("there is no practical resonance.")
       print ("There is too much damping.")
    print ("")
    print ("The amplitude of the steady periodic solution for")
    print ("the given w = " + str(N(w,digits=6)) + " is C(w) = " + str(N(1/(sqrt((2*w*p)^2+(1-w^2)^2)),digits=6)) + " (red lines)")
    sys.stdout.flush()
</script>
</div>


<hr>
<p>
The original code is mainly due to Ryan Burkhart.

<hr>
<p>
<a href="http://validator.w3.org/check/referer"><img src="/images/valid-html5.png" alt="Valid HTML 5" style="border:0; width:88px; height:31px;"></a>
<a href="http://jigsaw.w3.org/css-validator/check/referer"><img style="border:0;width:88px;height:31px" src="/images/vcss.png" alt="Valid CSS!"></a>

<!-- hack -->
<!--<br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br>
<br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br> <br>
-->
<div style="height:1000px; background-image:linear-gradient(white,black);"></div>

</body>
</html>
